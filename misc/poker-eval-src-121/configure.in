#
# If we do a ./configure --target=msdos, it will generate Makefile.dos 
# makefiles so that the package can be reasonably easily compiled with
# Visual C.  These files can then be rolled into the distribution.  
#

AC_INIT(include/poker_defs.h)

if test $target = "msdos"; then 
  CC=cl
  CC_OBJ="-c -Fo\$@ \$**"
  CC_EXE="-Fe\$@ \$**"
  CFLAGS="-O1 -nologo -DNDEBUG -MD -DMSDOS"
  A=".lib"
  O=".obj"
  E=".exe"
  SO=".dll"
  RM="del"
  MKDIR="mkdir"
  RUN_IT="\$**"
  LIB_CMD="LIB /OUT:\$@ \$**"
  SLIB_CMD="LINK /DLL /OUT:\$@ \$**"
  SLIBJNI_CMD="LINK /DLL /OUT:\$@ ..\\lib\\libpoker.dll \$**"
  RANLIB="rem"
else
  AC_CONFIG_HEADER(include/config.h)

  AC_CANONICAL_SYSTEM

  dnl Checks for programs.
  AC_PROG_RANLIB
  AC_PROG_CC

  dnl Checks for header files.
  AC_HEADER_STDC

  dnl Checks for typedefs, structures, and compiler characteristics.
  AC_C_CONST
  AC_C_INLINE
  AC_C_BIGENDIAN
  AC_CHECK_SIZEOF(long)
  AC_CHECK_SIZEOF(int)
  AC_CHECK_HEADERS(unistd.h)
  AC_CHECK_HEADERS(sys/stat.h)

  dnl Check for existence of "long long" type
  AC_MSG_CHECKING(if compiler supports long long integer types)
  AC_TRY_COMPILE([],
                 [ long long a; ],
                 [cf_longlong=yes],
                 [cf_longlong=no])
  AC_MSG_RESULT($cf_longlong)
  test $cf_longlong = yes && AC_DEFINE(HAVE_INT64)
  test $cf_longlong = yes && AC_DEFINE(HAVE_LONG_LONG)

  if test $GCC = yes; then 
    CFLAGS="-g -O4 -Wall -Winline -finline-functions -fomit-frame-pointer"
    case "$target" in
      # At one point, -m486 -malign-double showed better performance, but
      # now it doesn't appear to be so
      i?86-*) CFLAGS="$CFLAGS";;
    esac
  fi

  test -z "$enable_java" && enable_java=yes
  if test "$enable_java" = "yes" ; then
    dnl Check for java support.
    AC_CHECK_CLASSPATH
    AC_PROG_JAVAC
    AC_PROG_JAVA
    dnl AC_CHECK_RQRD_CLASS(org.apache.oro.text.regex.Pattern)
    dnl AC_CHECK_RQRD_CLASS(junit.framework.TestCase)
    AC_PATH_PROG(JAVAH,javah)
    AC_PATH_PROG(JAVADOC,javadoc)
    AC_PATH_PROG(JAR,jar)
  else
    enable_java=no
  fi
  ENABLE_JAVA="$enable_java"

  A=".a"
  O=".o"
  E=""
  SO=".so"
  RM="rm -f"
  MKDIR="mkdir"
  CC_OBJ="-c -o \$@ \$^"
  CC_EXE="-o \$@ \$^"
  RUN_IT="./\$<"
  LIB_CMD="\$(AR) cr \$@ \$^"
  # hardcoding -shared in SLIB_CMD seems wrong (e.g., it is -G with solaris cc)
  SLIB_CMD="\$(CC) -shared -o \$@ \$^"
  # more hardcoding...
  SLIBJNI_CMD="\$(CC) -shared -L../lib -lpoker -o \$@ \$^"
fi

AC_SUBST(CC)
AC_SUBST(CC_OBJ)
AC_SUBST(CC_EXE)
AC_SUBST(CFLAGS)
AC_SUBST(MKDIR)
AC_SUBST(RANLIB)
AC_SUBST(RM)

AC_SUBST(O)
AC_SUBST(E)
AC_SUBST(A)
AC_SUBST(SO)
AC_SUBST(RUN_IT)
AC_SUBST(LIB_CMD)
AC_SUBST(SLIB_CMD)
AC_SUBST(SLIBJNI_CMD)

AC_SUBST(ENABLE_JAVA)

AC_OUTPUT(Makefile lib/Makefile tests/Makefile examples/Makefile include/Makefile java/Makefile)

if test $target = "msdos"; then 
  mv lib/Makefile lib/Makefile.dos
  mv tests/Makefile tests/Makefile.dos
  mv examples/Makefile examples/Makefile.dos
  mv include/Makefile include/Makefile.dos
  mv java/Makefile java/Makefile.dos
fi
